#version 450

layout (local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

#define INFINITY 1.0f / 0.0f
#define EPSILON 0.001f

struct AmbientLight {
    float bright;
};

struct DirectionalLight {
	float bright, shininess;
	vec4 direction;
};

struct PointLight {
    float bright, maxT, shininess;
    vec4 position;
};

struct Sphere {
    float radius, specular, reflective;
    vec4 center;
    vec4 color;
};

struct Triangle {
    int single_side;
    float specular, reflective;
    vec4 A,B,C;
    vec4 color;
};

struct Ray {
    vec3 origin;
    vec3 direction;
};

struct AABB {
    vec4 min, max;
};

struct Hit {
    float t;
    bool is_sphere;
    bool is_triangle;
    bool is_aabb;
    Sphere sphere;
    Triangle triangle;
    AABB aabb;
};

struct Camera {
    vec3 position;
    mat4 rotation;
};

struct BVHNode {
    int parent_node;
    int left_node, right_node;
    int right_sibling;
    int triangle_index;
    AABB box;
};

layout (binding = 0, rgba8) uniform image2D colorBuffer;
layout (std140, binding = 1) readonly buffer SpheresBuffer {
    Sphere spheres[];
};
layout (binding = 2) uniform SceneBuffer {
    int spheres_num, triangles_num, point_lights_num, aa, bvh_origins, bvh_leaves;
    float ambient_bright;
    DirectionalLight directional_light;
    Camera camera;
} scene;
layout (std140, binding = 3) readonly buffer PointLightsBuffer {
    PointLight point_lights[];
};
layout (std140, binding = 4) readonly buffer TrianglesBuffer {
    Triangle triangles[];
};

layout (std140, binding = 5) readonly buffer BVHOriginsBuffer {
    BVHNode bvh_origins[];
};

layout (std140, binding = 6) readonly buffer BVHLeavesBuffer {
    BVHNode bvh_leaves[];
};

vec3 perPixel(int x, int y);

void main() {
    ivec2 screen_pos = ivec2(gl_GlobalInvocationID.x, gl_GlobalInvocationID.y);

    vec3 color = perPixel(screen_pos.x, screen_pos.y);

    imageStore(colorBuffer, screen_pos, vec4(color, 1.0));
}

vec2 getSphereHit(Ray ray, Sphere sphere) {
    vec3 oc = ray.origin - sphere.center.xyz;

    float a = dot(ray.direction, ray.direction);
    float b = 2.0 * dot(oc, ray.direction);
    float c = dot(oc, oc) - sphere.radius * sphere.radius;
    float discriminant = b*b - 4.0*a*c;

    if (discriminant > 0) {
        float x1 = (-b + sqrt(discriminant)) / (2 * a);
        float x2 = (-b - sqrt(discriminant)) / (2 * a);

        return vec2(x1, x2);
    }

    return vec2(INFINITY, INFINITY);
}

vec2 getTriangleHit(Ray ray, Triangle triangle) {
    vec3 e1 = (triangle.B - triangle.A).xyz;
    vec3 e2 = (triangle.C - triangle.A).xyz;

    vec3 pvec = cross(ray.direction, e2);
    float det = dot(e1, pvec);

    if (triangle.single_side == 1 && det < EPSILON) {
        return vec2(INFINITY, INFINITY);
    }
    else if (det < EPSILON && det > -EPSILON) {
        return vec2(INFINITY, INFINITY);
    }

    float inv_det = 1.0f / det;

    vec3 tvec = ray.origin - triangle.A.xyz;
    float v = inv_det * dot(tvec, pvec);

    if (v < 0.0f || v > 1.0f) {
        return vec2(INFINITY, INFINITY);
    }

    vec3 qvec = cross(tvec, e1);
    float w = inv_det * dot(ray.direction, qvec);

    if (w < 0.0f || w + v > 1.0f) {
        return vec2(INFINITY, INFINITY);
    }

    float t = inv_det * dot(e2, qvec);

    if (t > EPSILON) return vec2(t, t);

    return vec2(INFINITY, INFINITY);
}

vec2 getAABBHit(Ray ray, AABB box) {
    vec3 inv_direction = 1.0f / ray.direction;
    vec3 tMin = (box.min.xyz - ray.origin) * inv_direction;
    vec3 tMax = (box.max.xyz - ray.origin) * inv_direction;

    vec3 t1 = min(tMin, tMax);
    vec3 t2 = max(tMin, tMax);

    float tNear = max(t1.x, max(t1.y, t1.z));
    float tFar = min(t2.x, min(t2.y, t2.z));

    if (tNear > tFar || tFar < 0.0) {
        return vec2(INFINITY, INFINITY);
    }

    return vec2(tNear, tFar);
}

Ray getRay(float x, float y)
{
    ivec2 screen_size = imageSize(colorBuffer);
    float fov = 60;

    vec3 direction = vec3(x * 1.f / screen_size.x, y * 1.f / screen_size.y, -1.0f);

    direction.x = direction.x * 2.0f - 1.0f;
    direction.y = 1.0f - 2.0f * direction.y;

    float aspect_ratio = float(screen_size.x) / float(screen_size.y);
    float fov_radians = radians(fov);
    float tan_half_fov = tan(fov_radians / 2.0f);
    
    direction.x = direction.x * aspect_ratio * tan_half_fov;
    direction.y = direction.y * tan_half_fov;

    vec4 rotated_direction = scene.camera.rotation * vec4(direction, 1.0f);

    Ray ray;
    ray.origin = scene.camera.position;
    ray.direction = normalize(rotated_direction.xyz);

    return ray;
}

vec3 getSkyColor(Ray ray)
{
    vec3 base = vec3(0.4, 0.9, 1.0);

    float pos = 0.5 * (ray.direction.y + 1.0);

    return (pos * base) + ((1 - pos) * vec3(1.0, 1.0, 1.0));
}

vec3 GetPointLightVector(vec3 point, vec3 position) {
    return normalize(position - point);
}

Hit getTrianglesIntersection(Hit intersection, Ray ray, int start_index, int end_index, float min_t, float max_t) {
    for (int i = start_index; i < end_index; i++) {
		Triangle triangle = triangles[i];

		vec2 triangle_hit = getTriangleHit(ray, triangle);

		if (triangle_hit.x < intersection.t && min_t < triangle_hit.x && triangle_hit.x < max_t) {
			intersection.t = triangle_hit.x;
			intersection.triangle = triangle;
            intersection.is_triangle = true;
            intersection.is_sphere = false;
            intersection.is_aabb = false;
		}
		if (triangle_hit.y < intersection.t && min_t < triangle_hit.y && triangle_hit.y < max_t) {
			intersection.t = triangle_hit.y;
			intersection.triangle = triangle;
            intersection.is_triangle = true;
            intersection.is_sphere = false;
            intersection.is_aabb = false;
		}
	}

    return intersection;
}

/*Hit getAABBIntersection(Hit intersection, Ray ray) {
    for (int i = 0; i < scene.bvh_origins; i++) {
        BVHNode node = bvh_origins[i];

        vec2 hit = getAABBHit(ray, node.box);

        if (hit.x != INFINITY && hit.y != INFINITY) {
            intersection.is_sphere = false;
            intersection.is_triangle = false;
            intersection.is_aabb = true;
            intersection.aabb = node.box;
            intersection.t = hit.x;
        }
    }

    return intersection;
}*/

Hit getSphereIntersection(Hit intersection, Ray ray, float min_t, float max_t) {
    for (int i = 0; i < scene.spheres_num; i++) {
		Sphere sphere = spheres[i];

		vec2 sphere_hit = getSphereHit(ray, sphere);

		if (sphere_hit.x < intersection.t && min_t < sphere_hit.x && sphere_hit.x < max_t) {
			intersection.t = sphere_hit.x;
			intersection.sphere = sphere;
            intersection.is_triangle = false;
            intersection.is_sphere = true;
            intersection.is_aabb = false;
		}
		if (sphere_hit.y < intersection.t && min_t < sphere_hit.y && sphere_hit.y < max_t) {
			intersection.t = sphere_hit.y;
			intersection.sphere = sphere;
            intersection.is_triangle = false;
            intersection.is_sphere = true;
            intersection.is_aabb = false;
		}
	}

    return intersection;
}

Hit getBvhIntersection(Hit intersection, Ray ray, float min_t, float max_t) {
    bool found = false;
    float bvh_t = intersection.t;
    BVHNode origin;

    for (int i = 0; i < scene.bvh_origins; i++) {
        BVHNode origin_node = bvh_origins[i];

        vec2 hit = getAABBHit(ray, origin_node.box);

        if (hit.x == INFINITY && hit.y == INFINITY) {
            continue;
        }

        BVHNode node = bvh_leaves[origin_node.left_node];
        bool to_parent = false;

        // 1 - left nodes, 2 - right nodes
        while(true) {
            if (to_parent) {
                if (node.parent_node == -1 && node.right_sibling == -1) {
                    break;
                }

                node = bvh_leaves[node.parent_node];

                // if we come from left - we need check right nodes
                if (node.right_sibling != -1) {
                    to_parent = false;
                    node = bvh_leaves[node.right_sibling];
                // if we come from right - we checked all nodes below, and we need to get higher
                } else {
                    to_parent = true;
                    continue;
                }
            }

            vec2 aabb_hit;

            aabb_hit = getAABBHit(ray, node.box);

            // if hit box
            if (aabb_hit.x != INFINITY && aabb_hit.y != INFINITY) {
                // not last node with triangles
                if (node.left_node != -1 && node.right_node != -1) {
                    node = bvh_leaves[node.left_node];

                    continue;
                // node with triangles
                } else {
                    intersection = getTrianglesIntersection(
                        intersection, 
                        ray,
                        node.triangle_index, 
                        node.triangle_index + 1,
                        min_t,
                        max_t
                    );

                    if (node.right_sibling != -1) {
                        node = bvh_leaves[node.right_sibling];
                    } else {
                        to_parent = true;
                    }

                    continue;
                }
            // didn't hit box
            } else {
                if (node.right_sibling != -1) {
                    node = bvh_leaves[node.right_sibling];
                } else {
                    to_parent = true;
                }

                continue;
            }
        }
    }
    
    return intersection;
}

Hit getIntersection(Ray ray, float min_t, float max_t) {
	Hit intersection;
    intersection.t = INFINITY;
    intersection.is_sphere = false;
    intersection.is_triangle = false;
    intersection.is_aabb = false;

	intersection = getSphereIntersection(intersection, ray, min_t, max_t);

    intersection = getBvhIntersection(intersection, ray, min_t, max_t);

	return intersection;
}

vec3 getSphereNormal(vec3 point, Sphere sphere) {
    return normalize(point - sphere.center.xyz);
}

vec3 getTriangleNormal(Triangle triangle) {
    vec4 AB = triangle.B - triangle.A;
    vec4 AC = triangle.C - triangle.A;

    vec3 normal = normalize(cross(AB.xyz, AC.xyz));

    return normal;
}

vec3 getAABBNormal(vec3 point, AABB box) {
    vec3 normal;

    // Проверяем, с какой стороны AABB находится точка
    if (point.x < box.min.x + 0.001) {
        normal = vec3(-1.0, 0.0, 0.0); // Нормаль к грани слева
    } else if (point.x > box.max.x - 0.001) {
        normal = vec3(1.0, 0.0, 0.0); // Нормаль к грани справа
    } else if (point.y < box.min.y + 0.001) {
        normal = vec3(0.0, -1.0, 0.0); // Нормаль к грани снизу
    } else if (point.y > box.max.y - 0.001) {
        normal = vec3(0.0, 1.0, 0.0); // Нормаль к грани сверху
    } else if (point.z < box.min.z + 0.001) {
        normal = vec3(0.0, 0.0, -1.0); // Нормаль к грани сзади
    } else {
        normal = vec3(0.0, 0.0, 1.0); // Нормаль к грани спереди
    }

    return normal;
}

struct ObjectInfo {
    vec3 obj_color;
    float obj_specular;
};

ObjectInfo getObjectInfoByHits(Hit intersection) {
    ObjectInfo result;

    if (intersection.is_sphere) {
        result.obj_color = intersection.sphere.color.xyz;
        result.obj_specular = intersection.sphere.specular;
    } else if (intersection.is_triangle) {
        result.obj_color = intersection.triangle.color.xyz;
        result.obj_specular = intersection.triangle.specular;
    } else if (intersection.is_aabb) {
        result.obj_color = vec3(0.0, 1.0, 0.0);
        result.obj_specular = -1;
    }

    return result;
}

const float light_size = 0.025;
const vec3 offsets[8] = {
  vec3(-light_size, -light_size, -light_size),
  vec3(light_size, -light_size, -light_size),
  vec3(light_size, light_size, -light_size),
  vec3(-light_size, light_size, -light_size),
  vec3(-light_size, -light_size, light_size),
  vec3(light_size, -light_size, light_size),
  vec3(light_size, light_size, light_size),
  vec3(-light_size, light_size, light_size)
};

#define SHADOW_RAYS 1

float getShadowPointLight(
    vec3 point,
    vec3 normal,
    vec3 light_position
) {
    float intensity = 0.0f;

    for(int i = 0; i < SHADOW_RAYS; i++) {
        vec3 position = light_position + offsets[i];

        vec3 light_vector = GetPointLightVector(point, position);

        Ray shadow_ray;
        shadow_ray.origin = point + EPSILON * normal;
        shadow_ray.direction = light_vector;
        Hit shadow_blocker = getIntersection(shadow_ray, EPSILON, distance(point, position));
	    
        if (shadow_blocker.t != INFINITY) {
		    intensity += 1.0f;
	    }
    }

    intensity /= SHADOW_RAYS;

    return 1.0f - intensity;
}

float getShadowDirectionalLight(
    vec3 point,
    vec3 normal,
    vec3 light_vector
) {
    float intensity = 0.0f;

    for(int i = 0; i < SHADOW_RAYS; i++) {
        vec3 new_light_vector = light_vector + offsets[i];

        Ray shadow_ray;
        shadow_ray.origin = point + EPSILON * normal;
        shadow_ray.direction = new_light_vector;
        Hit shadow_blocker = getIntersection(shadow_ray, EPSILON, INFINITY);
	    
        if (shadow_blocker.t != INFINITY) {
		    intensity += 1.0f;
	    }
    }

    intensity /= SHADOW_RAYS;

    return 1.0f - intensity;
}

vec3 CalculatePhongLight(
    vec3 light_vector, vec3 view, vec3 normal, 
    ObjectInfo obj_info, float shininess, float bright,
    float shadow_intensity
) {
    vec3 light_color = vec3(1.0f); 
    vec3 specular_color = vec3(0.0);

    vec3 diffuse_color = light_color * max(dot(normal, light_vector), 0.0f) * bright;

    if (obj_info.obj_specular != -1.0f) {
        vec3 reflected = -reflect(light_vector, normal);
        float r_dot_v = max(dot(reflected, view), 0.0f);
        float specular_strength = pow(r_dot_v, shininess);

        specular_color = obj_info.obj_specular * specular_strength * light_color;
    }

    return shadow_intensity * (diffuse_color + specular_color) * obj_info.obj_color;
}

vec3 ComputeLight(
    vec3 point,
    vec3 normal,
    vec3 view,
    Hit intersection
) {
    vec3 final_color = vec3(0.0f);

    ObjectInfo object_info = getObjectInfoByHits(intersection);

    vec3 direction_light_position = scene.directional_light.direction.xyz;

    float shadow_intensity = getShadowDirectionalLight(point, normal, direction_light_position);

    final_color += CalculatePhongLight(
        direction_light_position,
        view, normal,
        object_info, scene.directional_light.shininess, 
        scene.directional_light.bright, shadow_intensity
    );

    for (int i = 0; i < scene.point_lights_num; i++) {
        PointLight point_light = point_lights[i];
        vec3 light_position = point_light.position.xyz;

        vec3 light_vector = GetPointLightVector(point, light_position);

        float shadow_intensity = getShadowPointLight(point, normal, light_position);

        final_color += CalculatePhongLight(
            light_vector, view, normal,
            object_info, point_light.shininess, point_light.bright, shadow_intensity
        );
    }

    final_color += scene.ambient_bright * object_info.obj_color;

    return final_color;
}

vec3 traceRay(
    Ray ray,
    float min_t,
    float max_t,
    int reflection_depth
) {
    vec3 final_color = vec3(0.0);
    float reflection_intensity = 1.0;

    for (int i = 0; i < reflection_depth; i++) {
        Hit intersection = getIntersection(ray, min_t, max_t);

        if (intersection.t == INFINITY) {
            final_color += reflection_intensity * getSkyColor(ray);
            break;
        }

        vec3 point = ray.origin + (ray.direction * intersection.t);
        vec3 normal;

        if (intersection.is_sphere) {
            normal = getSphereNormal(point, intersection.sphere);
        } else if(intersection.is_triangle) {
            normal = getTriangleNormal(intersection.triangle);
        } else if(intersection.is_aabb) {
            normal = getAABBNormal(point, intersection.aabb);
        }

        vec3 view = ray.direction * -1.0;

        vec3 local_color = ComputeLight(point, normal, view, intersection);

        final_color += reflection_intensity * local_color;

        float reflective;

        if (!intersection.is_triangle) {
            reflective = intersection.sphere.reflective;
        } else {
            reflective = intersection.triangle.reflective;
        }

        if (reflective > 0.0f) {
            ray.origin = point + EPSILON * normal;
            ray.direction = normalize(reflect(ray.direction, normal));
            reflection_intensity *= reflective;
        } else {
            break;
        }
    }

    return final_color;
}

vec3 getPixelColor(float x, float y)
{
    Ray ray = getRay(x, y);

    vec3 color = traceRay(ray, 1, INFINITY, 3);

    return color;
}


vec3 perPixel(int x, int y)
{
	vec3 result_ns;

    if (scene.aa <= 1) {
        result_ns = getPixelColor(x, y);
    } else {
        vec3 color = vec3(0.0f);

        float half_aa = scene.aa / 2.0f;
	    float partial_aa = 1.f / half_aa;

        float pixel_start_x = x - 0.5f;
	    float pixel_start_y = y - 0.5f;

        for (int i = 0; i < half_aa; i++) {
		    float u = pixel_start_x + (partial_aa * i);
		    float v = pixel_start_y + (partial_aa * i);

		    color = color + getPixelColor(u, v);
	    }

	    pixel_start_x = x + 0.5f;

        for (int i = 0; i < half_aa; i++) {
	        float u = pixel_start_x - (partial_aa * i);
		    float v = pixel_start_y + (partial_aa * i);

		    color = color + getPixelColor(u, v);
	    }

	    result_ns = color / scene.aa;
    }

    return result_ns;
}